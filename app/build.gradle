apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "com.example.workout.timer"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 2
        versionName "1.0.1"
        setProperty("archivesBaseName", "Workout-Timer-v${versionName}")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "@string/app_name_debug"
            //https://developer.android.com/studio/build/gradle-tips
            //Constant val for rest time
            buildConfigField "Long", "COUNTDOWN_PULL_UP", "5L" //0m:05s
            buildConfigField "Long", "COUNTDOWN_PRESS", "3L"   //0m:03s
            buildConfigField "Long", "COUNTDOWN_BARS", "4L"    //0m:04s
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            resValue "string", "app_name", "@string/app_name_release"
            //Constant val for rest time
            buildConfigField "Long", "COUNTDOWN_PULL_UP", "(2 * 60) + 50L" //2m:50s
            buildConfigField "Long", "COUNTDOWN_PRESS", "50L"              //0m:50s
            buildConfigField "Long", "COUNTDOWN_BARS", "(1 * 60) + 50L"    //1m:50s
        }
    }

    //https://developer.android.com/studio/build/build-variants
    flavorDimensions 'api'
    productFlavors {
        Oreo {
            dimension 'api'
            minSdkVersion 27
            versionCode 20000 + android.defaultConfig.versionCode
            versionNameSuffix '-minApi27-8.1.0-Oreo'
        }

        KitKat {
            dimension 'api'
            minSdkVersion 19
            versionCode 10000 + android.defaultConfig.versionCode
            versionNameSuffix '-minApi19-4.4.x-KitKat'
        }
    }
}

dependencies {
}
